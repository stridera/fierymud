#18500
prior_drop_head~
0 f 100
~
if %actor.vnum% == -1
set drophead 0
if %actor.class% /= Warrior
set drophead 1
endif
if %actor.class% /= Cleric
set drophead 1
endif
if %actor.class% /= Sorcerer
set drophead 1
endif
if %actor.level% < 10
set drophead 0
else
if %actor.level% >  40
set drophead 0
endif
endif
if %drophead% == 1
mload obj 8504
endif
endif
~
#18501
silvania_welcome~
0 g 100
~
say Some know not of their destinies, others simply choose to ignore them.
say Which of the two are you?
ponder %actor.name%
~
#18502
quest_preamble_silania~
0 d 100
destiny destiny?~
if %actor.vnum% == -1
if %start_q% > 0
if %q_plyr% /= %actor.name%
poke %q_plyr%
say You are already on the way to fulfilling your destiny %q_plyr%.
else
say Sorry little one, I am already running one quest, come back later.
endif
else
if %actor.align% > 349
nod %actor.name%
if %actor.class% /= Warrior
say Do ye wish to join the ranks of the holy warriors?
set use_subclass Pal
set start_q 1
else
if %actor.class% /= Cleric
say Do ye wish to join the ranks of the priests and help drive darkness from our world?
set use_subclass Pri
set start_q 1
else
say Sorry, I cannot help you achieve your destiny.
endif
endif
if %start_q% == 1
global start_q
set q_plyr %actor.name%
global q_plyr
global use_subclass
endif
else
say I cannot help you if you are not good, little one.
endif
endif
~
#18503
start_quest_silania~
0 d 100
yes no~
* since silania is nice, should we give more chances to quit quest? start_q > 1
if %actor.vnum% == -1
if %q_plyr% /= %actor.name%
if %start_q% == 1
if %speech% /= yes
quest start pri_pal_subclass %actor.name% %use_subclass%
unset start_q
unset q_plyr
smile %actor.name%
say It is necessary to make a quest such as this quite tough to ensure you really want to do this.
wait 2
say I am sure you will complete the quest though.
else
unset start_q
unset q_plyr
unset use_subclass
sigh
say It is a shame we have so few willing to join us these days, but I cannot compel you.
say Farewell, and I hope you come to reconsider your decision sometime.
wait 2
msend %actor% Silania makes a gesture with her hand and you are blinded for a second.
mechoaround %actor% Silania makes a gesture with her hand and %actor.name% disappears.
mteleport %actor% 18566
endif
endif
endif
endif
~
#18504
quest_details_silania~
0 d 100
quest quest?~
if %actor.vnum% == -1
if %actor.quest_stage[pri_pal_subclass]% == 1
quest advance pri_pal_subclass %actor.name%
say At this abbey we have always permitted any person to wander freely.
wait 1
ponder
wait 1
say Well, not if they are evilly aligned, but otherwise...
say One of our guests made off with our most sacred bronze chalice.
steam
wait 2
say We have reason to believe it was a ruse by the filthy Diabolists to try and weaken us.
say Our Prior has offered to try and retrieve it for us, but
wait 2
think
say I think perhaps that would be a bad idea, and that you should find it.
endif
endif
~
#18505
give_chalice_silania~
0 j 100
~
if %actor.vnum% == -1
wait 2
if %object.vnum% == 18512
* stage is set to 3 when chalice retrieved
if %actor.quest_stage[pri_pal_subclass]% == 3
emote beams a huge smile.
wait 3
hug %actor.name%
You have done me a great service, and earned your place among the truly holy.
   smi
   wait 2
   say Type 'subclass' to proceed.
quest complete pri_pal_subclass %actor.name%
   else
   * not in final stage of quest
if %actor.quest_stage[pri_pal_subclass]% == 1
   say Hmmm, jumping the gun a bit aren't we? I didn't even tell you the quest yet.
   else
say Thank you greatly for returning this.
   thank %actor.name%
   wait 2
if %actor.quest_stage[pri_pal_subclass]% == 2
* hmm..they got the chalice without running the appropriate trigger
* could they be cheating?
say Hmm..I don't know how you got hold of this, but I will give you the benefit of the doubt and not fail you.
poke %actor.name%
wait 5
say Now..go and get the chalice yourself properly!
else
Shame you were not performing the quest, your selfless act would have qualified you for our holy brotherhood.
endif
   endif
   endif
* junk the chalice
mjunk chalice
   endif
endif
~
#18506
exit_from_silania~
0 d 100
exit exit?~
say Ooops, how embarassing, I forgot about the door!
m_run_room_trig 18507
~
#18507
create_door_18547_to_18566~
2 g 0
~
* only run from silanias trigger 18506
wait 2
wecho The walls seem to flow away from an opening.
wdoor 18547 east room 18566
wdoor 18566 west room 18547
~
#18508
close_door_18547_to_18566~
2 g 100
~
wait 5
set closeit 0
if %direction% /= east
if %self.vnum% == 18547
set closeit 1
endif
endif
if %direction% /= west
if %self.vnum% == 18566
set closeit 1
endif
endif
if %closeit% == 1
wecho The walls seem to flow together behind you, sealing the entrance!
wdoor 18547 east purge
wdoor 18566 west purge
endif
~
#18510
abbot_rand1~
0 b 10
~
fume
say Not another blackmail note about my prior.
emote rips up a letter into tiny pieces.
stomp
~
#18511
abbot_speak1~
0 d 100
prior prior?~
peer %actor.name%
say Anything you heard about Berack is pure hearsay.
sigh
say That young man has enough to worry about without people bringing up his family all the time.
~
#18512
abbot_rand2~
0 b 10
~
emote mutters about the tribulations of monastic life.
sigh
~
#18513
chalice_retrieval~
1 g 100
~
if %self.room% == 8591
* we are in chalice room so set the quest bit
if %actor.quest_stage[pri_pal_subclass]% == 2
if %already_got% == 1
oechoaround %actor% The chalice slips from %actor.p% fingers!
osend %actor% The chalice slips from your fingers!
return 0
else
* need oforce until i allow objects to use the quest cmd!
quest advance pri_pal_subclass %actor.name%
endif
endif
set already_got 1
global already_got
endif
~
#18514
chalice_dropped~
1 h 100
~
* if the chalice is dropped it could be some1 trying to get round
* the GET trigger...this should prevent that
oecho the DROP trigger for %self.name% is RUNNING
~
#18515
almoner_give_food~
0 b 25
test~
set victim %random.char%
if (%victim.vnum% == -1) && (%victim.level% < 25) && (%victim.can_be_seen%)
   smile %victim.name%
   say Hello there, young one.  You look like you could use a bite to eat.
   mjunk waybread
   mload obj 18508
   give waybread %victim.name%
endif
~
#18566
kneel_door_open~
2 c 100
kneel~
return 0
if %actor.align% > -349
wsend %actor.name% A bright white beam of light descends upon you.
wechoaround %actor.name% A bright beam of white light descends upon %actor.name%.
wdoor 18566 west flags a
wdoor 18566 west room 18547
wdoor 18566 west name large wooden door
wdoor 18566 west key -1
wdoor 18566 west description A large wooden door bars your way.
wait 3
wsend %actor.name% A door in the cross to the west opens silently.
wechoaround %actor.name% A door in the cross to the west opens silently.
else
wsend %actor.name% A bright white beam of light descends upon you.
wechoaround %actor.name% A bright white beam of light descends upon %actor.name%.
wait 2
wsend %actor.name% The light becomes increasingly bright, and turns painful!
wdamage %actor.name% 179
wechoaround %actor.name% The light brightens significantly, burning %actor.name%'s skin.
end
~
#18567
kneel_normalizer~
2 c 100
k~
return 0
~
$~
